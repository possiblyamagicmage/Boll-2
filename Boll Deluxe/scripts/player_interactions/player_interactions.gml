function player_interactions(){
	if (piped) exit
	
	var enemystomp=check_rectangle_in_hitbox(x-hit_sizex,y+hit_sizey+1,x+hit_sizex,y+hit_sizey+1, oEnemy)
	if (enemystomp) && (enemystomp.phaseid==noone || enemystomp.phaseid.id!=id) && !(enemystomp.damage_on_contact) && !(enemystomp.no_stomping) && !grounded && vsp > 0  {
		if !(hurt) && !(dead) 
		enemystomp.enemyStomped.Emit(id);
	} else {
		var enemy=check_hitbox_on_hitbox(id, oEnemy)
		if (enemy) && (enemy.phaseid==noone || enemy.phaseid.id!=id) && !(hurt) && !(dead) {
			enemy.enemyCollidePlayer.Emit(id);
		}
	}
	
	var spring = collision_line(x-hit_sizex,y+hit_sizey,x+hit_sizex,y+hit_sizey, oTerrainSpring, false, true)
	if (spring) && !(hurt) && !(dead)  {
		vsp=min(-spring.spring_power,vsp) //dont set vsp if it exceeds power
		grounded = false
		spring.image_speed=1
		sig.Emit("sprung")
		if (spring.object_index == oPollenFlower) {
			pollenated=true
			if !part_system_exists(pollenPart) {
				pollenPart=part_system_create(pPollenParticles);
				part_system_depth(pollenPart,-1)
				part_system_position(pollenPart,x+8,y+8)
				part_system_global_space(pollenPart,true)
			}
		}
	}
	
	var amp = collision_rectangle(x-hit_sizex,y-hit_sizey,x+hit_sizex,y+hit_sizey, oAmp, false, true)
	if (amp) && !(electrocuted) && !(hurt) && !(dead) {
		if !(invincible_type && invincible_timer) {
			sig.Emit("electrocute");
		}
	}
	
	var list=ds_list_create();
	var num=collision_line_list(x-(hit_sizex),y+hit_sizey+1,x+(hit_sizex),y+hit_sizey+1, oHittable, false, true, list, true)
	if (num > 0) {
		var totaldy=0;
		var i=0;
		repeat (num) {
			var hittable=list[| i]
			if !(hurt) && !(dead)  {
				if(abs(hittable.dy) > abs(totaldy)) {
					totaldy=hittable.dy //get the greatest block's dy
				}
		
				if (hittable.object_index == oNoteBlock) {
					if !(hittable.do_bump) {
						hittable.blockHit.Emit(1, id);
						grounded=false;
						canstopjump = true
						vsp=-5-akey*1.5
					}
				}
			}
			i++;
		}
		dy=totaldy
	} else dy=0
	
	#region Solid Spike Collision
	var spike=collision_line(x-hit_sizex+hsp,y-hit_sizey+2,x-hit_sizex+hsp,y+hit_sizey-abs(vsp), oSolidSpike, false, true)
	if (spike) && (spike.dir="right" || spike.dir="none") && !(hurt) {
		if !(invincible_type && invincible_timer) {
			sig.Emit("hurt_by_spike")
		}
	}
	
	var spike=collision_line(x+hit_sizex+hsp,y-hit_sizey+2,x+hit_sizex+hsp,y+hit_sizey-abs(vsp), oSolidSpike, false, true)
	if (spike) && (spike.dir="left" || spike.dir="none") && !(hurt) {
		if !(invincible_type && invincible_timer) {
			sig.Emit("hurt_by_spike")
		}
	}
	
	var spike=collision_line(x-hit_sizex-1,y+hit_sizey+1,x+hit_sizex-1,y+hit_sizey+1, oSolidSpike, false, true)
	if (spike) && (spike.dir="up" || spike.dir="none") && !(hurt) {
		if !(invincible_type && invincible_timer) {
			sig.Emit("hurt_by_spike")
		}
	}
	
	var spike=collision_line(x-hit_sizex-1,y-hit_sizey+vsp,x+hit_sizex-1,y-hit_sizey+vsp, oSolidSpike, false, true)
	if (spike) && (spike.dir="down" || spike.dir="none") && !(hurt) {
		if !(invincible_type && invincible_timer) {
			sig.Emit("hurt_by_spike")
		}
	}
	#endregion
	
	var chainsaw=collision_rectangle(x-hit_sizex,y-hit_sizey,x+hit_sizex,y+hit_sizey, oChainsaw, false, true)
	if (chainsaw) && !(hurt) {
		if !(invincible_type && invincible_timer) {
			sig.Emit("hurt_by_spike")
		}
	}
	
	var mysteryorb=collision_rectangle(x-hit_sizex,y-hit_sizey,x+hit_sizex,y+hit_sizey, oMysteryOrb, false, true)
	if (mysteryorb) && !(finish) {
		instance_destroy(mysteryorb);
		finish=1;
	}
	
	var steely = collision_rectangle(x-hit_sizex,y-(hit_sizey-1),x+hit_sizex,y+(hit_sizey-1), oBigSteely, true, true)
	if (steely) && !(hurt) && !(dead) {
		if !(invincible_type && invincible_timer) {
			sig.Emit("hurt_by_spike");
		}
	}
	
	var flagpole=collision_rectangle(x-hit_sizex,y-hit_sizey,x+hit_sizex,y+hit_sizey, oFlagpole, false, true)
	if (flagpole) && !(finish) && !(dead) {
		if (flagpole.state == 0) {
			finish = 1;
			flagpole.state = 1;
			flagpole.player = id;
		}
	}
	
	var bearballoon=collision_rectangle(x-hit_sizex,y-hit_sizey,x+hit_sizex,y+hit_sizey, oPolarBearBalloon, false, true)
	if (bearballoon) && !(hurt) && !(dead) {
		if vsp >= 0 vsp=-(2.5+akey*1.5);
		sig.Emit("sprung");
		instance_create_depth(bearballoon.x,bearballoon.y,2,pImpact)
		instance_destroy(bearballoon);
	}
	
	var crate=collision_line(x-(hit_sizex-1),y+hit_sizey+vsp,x+(hit_sizex-1),y+hit_sizey+vsp, oCrate, false, true) 
	if (crate) && (vsp>=0) && !(hurt) && !(dead) {
		vsp = -(2.5+akey*1.5);
		sig.Emit("sprung");
		crate.blockHit.Emit(-1, id);
	}
	
	var coin=collision_rectangle(x-hit_sizex,y-hit_sizey,x+hit_sizex,y+hit_sizey, oCoin, false, true)
	if (coin) && !(hurt) && !(dead) {
		global.coins_collected++;
		VinylPlay(snd_itemcoin);
		instance_create_depth(coin.x,coin.y,0,pGlitter);
		instance_destroy(coin);
	}
	
	var item=collision_rectangle(x-hit_sizex,y-hit_sizey,x+hit_sizex,y+hit_sizey, oMushroom, false, true)
	if (item) && !(hurt) && !(dead) {
		item.itemCollected.Emit(id);
	}
	
	var monitor=collision_line(x-hit_sizex,y+hit_sizey+vsp,x+hit_sizex,y+hit_sizey+vsp, oMonitor, false, true)
	if (monitor) && !(hurt) && !(dead) {
		monitor.blockHit.Emit(-1, id);
	}
}